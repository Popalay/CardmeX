apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "androidx.navigation.safeargs"
apply plugin: "io.fabric"
apply from: "$project.rootDir/scripts/version.gradle"

android {
    compileSdkVersion Versions.compileSdkVersion
    defaultConfig {
        applicationId "com.popalay.cardme"
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionName Versions.versionName
        versionCode VERSION_CODE.toInteger()
    }

    signingConfigs {
        debug {
            storeFile file(Config.debugStoreFile)
        }

        release {
            storeFile file(Config.releaseStoreFile)
            keyAlias Config.releaseKeyAlias
            keyPassword Config.releaseKeyPassword
            storePassword Config.storePassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "type", "env"
    productFlavors {

        dev {
            dimension "env"
            versionNameSuffix "-dev"
        }

        prod {
            dimension "env"
        }

        x {
            dimension "type"
            applicationIdSuffix ".x"
            resValue "string", "app_name", "CardmeX"

            signingConfigs {

                debug {
                    storeFile file(Config.debugStoreFile)
                }

                release {
                    storeFile file(Config.releaseStoreFile)
                    keyAlias Config.releaseXKeyAlias
                    keyPassword Config.releaseXKeyPassword
                    storePassword Config.storePassword
                }
            }
        }

        full {
            dimension "type"
            resValue "string", "app_name", "Cardme"
        }
    }
}

dependencies {
    implementation project(":core")
    implementation project(":features:main")
    implementation project(":injector")

    implementation Libs.supportDesign
    implementation Libs.supportLib

    implementation Libs.navigationFragment
    implementation Libs.navigationUi

    implementation Libs.firebaseCore
    implementation Libs.androidProguards
    implementation(Libs.crashlytics) {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'