apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "androidx.navigation.safeargs"
apply plugin: "io.fabric"
apply from: "$rootDir/scripts/test_config.gradle"
apply from: "$rootDir/scripts/jococo.gradle"

android {
    compileSdkVersion Versions.compileSdkVersion
    defaultConfig {
        applicationId "com.popalay.cardme"
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionName Versions.versionName
        versionCode Versions.versionCode
    }

    signingConfigs {
        debug {
            storeFile file(SignIn.debugStoreFile)
        }

        release {
            storeFile file(SignIn.releaseStoreFile)
            keyAlias SignIn.releaseKeyAlias
            keyPassword SignIn.releaseKeyPassword
            storePassword SignIn.storePassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "type", "env"
    productFlavors {

        dev {
            dimension "env"
            versionNameSuffix "-dev"
            ext.enableCrashlytics = false
        }

        prod {
            dimension "env"
        }

        x {
            dimension "type"
            applicationIdSuffix ".x"
            resValue "string", "app_name", "CardmeX"
            buildConfigField "boolean", "ENABLE_BUG_REPOTRING", "true"

            signingConfigs {

                debug {
                    storeFile file(SignIn.debugStoreFile)
                }

                release {
                    storeFile file(SignIn.releaseStoreFile)
                    keyAlias SignIn.releaseXKeyAlias
                    keyPassword SignIn.releaseXKeyPassword
                    storePassword SignIn.storePassword
                }
            }
        }

        full {
            dimension "type"
            resValue "string", "app_name", "Cardme"
            buildConfigField "boolean", "ENABLE_BUG_REPOTRING", "false"
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.0'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation project(":core")
    implementation project(":features:main")
    implementation project(":injector")
    implementation project(":devtools:shaketoreport")

    implementation Libs.navigationFragment
    implementation Libs.navigationUi
    implementation Libs.lifecycle

    implementation Libs.firebaseCore
    implementation Libs.crashlytics

    implementation Libs.stetho
}

apply plugin: "com.google.gms.google-services"