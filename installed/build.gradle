apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "androidx.navigation.safeargs"
apply plugin: "io.fabric"
apply from: "$rootDir/scripts/test_config.gradle"
apply from: "$rootDir/scripts/jococo.gradle"

android {
    compileSdkVersion Versions.compileSdkVersion
    defaultConfig {
        applicationId "com.popalay.cardme"
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionName Versions.versionName
    }

    signingConfigs {
        debug {
            storeFile file(SignIn.debugStoreFile)
        }

        release {
            storeFile file(SignIn.releaseStoreFile)
            keyAlias SignIn.releaseKeyAlias
            keyPassword SignIn.releaseKeyPassword
            storePassword SignIn.storePassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "type", "env", "experience"
    productFlavors {

        dev {
            dimension "env"
            versionNameSuffix "-dev"
            ext.enableCrashlytics = false
        }

        prod {
            dimension "env"
        }

        x {
            dimension "type"
            applicationIdSuffix ".x"
            resValue "string", "app_name", "CardmeX"

            signingConfigs {

                debug {
                    storeFile file(SignIn.debugStoreFile)
                }

                release {
                    storeFile file(SignIn.releaseStoreFile)
                    keyAlias SignIn.releaseXKeyAlias
                    keyPassword SignIn.releaseXKeyPassword
                    storePassword SignIn.storePassword
                }
            }
        }

        full {
            dimension "type"
            resValue "string", "app_name", "Cardme"
        }

        instant {
            dimension "experience"
            versionCode Versions.versionCode
        }

        installed {
            dimension "experience"
            versionCode Versions.versionCode + 1
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.0'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation project(":core")
    implementation project(":features:main")
    implementation project(":features:cardlist")
    implementation project(":features:addcard")
    implementation project(":features:usercard")
    implementation project(":features:cardactions")
    implementation project(":features:carddetails")
}

apply plugin: "com.google.gms.google-services"